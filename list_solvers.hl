(* ========================================================================= *)
(* Examples with list of numbers.                                            *)
(* ========================================================================= *)

needs "kanren-light/solvers.hl";;

type_invention_warning := false;;
needs "Permutation/make.ml";;
type_invention_warning := true;;

prioritize_num();;

(* ------------------------------------------------------------------------- *)
(* Horn clauses for basic arithmetic operations.                             *)
(* ------------------------------------------------------------------------- *)

let ARITH_LE_HORN = prove
 (`(!m n. m <= n ==> NUMERAL m <= NUMERAL n) /\
   _0 <= _0 /\
   (!n. n <= _0 ==> BIT0 n <= _0) /\
   (!n. _0 <= BIT0 n) /\
   (!n. _0 <= BIT1 n) /\
   (!m n. m <= n ==> BIT0 m <= BIT0 n) /\
   (!m n. m <= n ==> BIT0 m <= BIT1 n) /\
   (!m n. m < n ==> BIT1 m <= BIT0 n) /\
   (!m n. m <= n ==> BIT1 m <= BIT1 n) /\
   (!m n. n < m ==> ~(m <= n)) /\
   (!m n. n <= m ==> m >= n) /\
   (!m n. m < n ==> ~(m >= n))`,
  REWRITE_TAC[GE; NOT_LE; ARITH_LE]);;

let ARITH_LT_HORN = prove
 (`(!m n. m < n ==> NUMERAL m < NUMERAL n) /\
   (!n. _0 < n ==> _0 < BIT0 n) /\
   (!n. _0 < BIT1 n) /\
   (!m n. m < n ==> BIT0 m < BIT0 n) /\
   (!m n. m <= n ==> BIT0 m < BIT1 n) /\
   (!m n. m < n ==> BIT1 m < BIT0 n) /\
   (!m n. m < n ==> BIT1 m < BIT1 n) /\
   (!m n. n <= m ==> ~(m < n)) /\
   (!m n. n < m ==> m > n) /\
   (!m n. m <= n ==> ~(m > n))`,
  REWRITE_TAC[GT; NOT_LT; ARITH_LT]);;

let ARITH_SUC_HORN = prove
 (`(!n m. SUC n = m ==> SUC (NUMERAL n) = NUMERAL m) /\
   SUC _0 = BIT1 _0 /\
   (!n. SUC (BIT0 n) = BIT1 n) /\
   (!n m. SUC n = m ==> SUC (BIT1 n) = BIT0 m)`,
  REWRITE_TAC[ARITH_SUC] THEN REPEAT STRIP_TAC THEN
  POP_ASSUM SUBST_VAR_TAC THEN REWRITE_TAC[ARITH_SUC]);;

let ARITH_SLV : solver =
  PROLOG_SLV NO_CONV [ARITH_LE_HORN; ARITH_LT_HORN; ARITH_SUC_HORN];;

(* Examples. *)
let tst tm =
  let [_,th] = take 10 (solve ARITH_SLV tm) in
  assert (th = ARITH_RULE tm) in
do_list tst
  [`2 <= 3`; `2 < 3`; `2 <= 2`; `~(3 <= 2)`;
   `3 >= 2`; `3 > 2`; `2 >= 2`; `~(2 >= 3)`;
   `SUC 3 = 4`];;

(* ------------------------------------------------------------------------- *)
(* Length.                                                                   *)
(* ------------------------------------------------------------------------- *)

let NUM_LENGTH_HORN = prove
 (`LENGTH ([]:num list) = 0 /\
  (!h:num t n m. LENGTH t = m /\ SUC m = n ==> LENGTH (CONS h t) = n)`,
  REWRITE_TAC[LENGTH] THEN MESON_TAC[]);;

(* With type instantiation. *)

let LENGTH_HORN = prove
 (`LENGTH ([]:A list) = 0 /\
  (!h:A t n m. LENGTH t = m /\ SUC m = n ==> LENGTH (CONS h t) = n)`,
  REWRITE_TAC[LENGTH] THEN MESON_TAC[]);;

(* ------------------------------------------------------------------------- *)
(* Append.                                                                   *)
(* ------------------------------------------------------------------------- *)

let APPEND_HORN = prove
 (`(!l:A list. APPEND [] l = l) /\
   (!x:A xs ys zs. APPEND xs ys = zs ==> APPEND (CONS x xs) ys = CONS x zs)`,
  REWRITE_TAC[APPEND; CONS_11]);;

let APPEND_NIL_CLAUSE, APPEND_CONS_CLAUSE = CONJ_PAIR APPEND_HORN;;

let NUM_APPEND_HORN = INST_TYPE [`:num`,aty] APPEND_HORN;;

let NUM_APPEND_NIL, NUM_APPEND_CONS = CONJ_PAIR NUM_APPEND_HORN;;

(* ------------------------------------------------------------------------- *)
(* Reverse.                                                                  *)
(* ------------------------------------------------------------------------- *)

let REVERSE_HORN = prove
 (`(REVERSE [] = []:A list) /\
   (!x:A xs ys zs. REVERSE xs = ys /\ APPEND ys [x] = zs
                   ==> REVERSE (CONS x xs) = zs)`,
  REWRITE_TAC[REVERSE] THEN MESON_TAC[]);;

let NUM_REVERSE_HORN = INST_TYPE [`:num`,aty] REVERSE_HORN;;

let NUM_REVERSE_NIL, NUM_REVERSE_CONS = CONJ_PAIR NUM_REVERSE_HORN;;

(* ------------------------------------------------------------------------- *)
(* Filter.                                                                   *)
(* ------------------------------------------------------------------------- *)

let FILTER_HORN = prove
 (`(!P:A->bool. FILTER P [] = []) /\
   (!P x:A xs ys. P x /\ FILTER P xs = ys
                  ==> FILTER P (CONS x xs) = CONS x ys) /\
   (!P x:A xs ys. ~P x /\ FILTER P xs = ys ==> FILTER P (CONS x xs) = ys)`,
  REWRITE_TAC[FILTER] THEN REPEAT STRIP_TAC THEN COND_CASES_TAC THEN
  ASM_REWRITE_TAC[] THEN ASM_MESON_TAC[]);;

let NUM_FILTER_HORN = INST_TYPE [`:num`,aty] FILTER_HORN;;

let solve_filter =
  solve (PROLOG_SLV NO_CONV [ARITH_LE_HORN; ARITH_LT_HORN; NUM_FILTER_HORN]);;

(* Examples. *)

take 10 (solve_filter `??ys. FILTER ((>=) 3) [] = ys`);;
take 10 (solve_filter `??ys. FILTER ((>=) 3) [1] = ys`);;
take 10 (solve_filter `??ys. FILTER ((>=) 3) [5] = ys`);;
take 10 (solve_filter `??ys. FILTER ((>=) 3) [1;5;2] = ys`);;

(* With type instantiation. *)

let solve_filter =
  solve (PROLOG_SLV NO_CONV [ARITH_LE_HORN; ARITH_LT_HORN; FILTER_HORN]);;

take 10 (solve_filter `??ys. FILTER ((>=) 3) [] = ys`);;
take 10 (solve_filter `??ys. FILTER ((>=) 3) [1] = ys`);;
take 10 (solve_filter `??ys. FILTER ((>=) 3) [5] = ys`);;
take 10 (solve_filter `??ys. FILTER ((>=) 3) [1;5;2] = ys`);;

(* ------------------------------------------------------------------------- *)
(* Sort.                                                                     *)
(* ------------------------------------------------------------------------- *)

let SORT = new_definition
  `SORT le xs ys <=> xs:A list PERMUTED ys /\ ORDERED le ys`;;

let SORT_NIL = prove
 (`!le. SORT (le:A->A->bool) [] []`,
  REWRITE_TAC[SORT; PERMUTED_RULES; ORDERED_RULES]);;

let QUICKSORT_HORN = prove
 (`!le:A->A->bool.
     (!x y. le x y \/ le y x) /\
     (!x y z. le x y /\ le y z ==> le x z)
     ==> SORT le [] [] /\
         (!x xs ys xs1 xs2 ys1 ys2.
            FILTER (\y. le y x) xs = xs1 /\
            SORT le xs1 ys1 /\
            FILTER (\y. ~le y x) xs = xs2 /\
            SORT le xs2 ys2 /\
            APPEND ys1 (CONS x ys2) = ys
            ==> SORT le (CONS x xs) ys)`,
  REWRITE_TAC[SORT_NIL] THEN INTRO_TAC "!le; total trans" THEN
  REPEAT GEN_TAC THEN INTRO_TAC "fil1 sor1 fil2 sor2 app" THEN
  REPEAT (FIRST_X_ASSUM SUBST_VAR_TAC) THEN
  REWRITE_TAC[SORT] THEN CONJ_TAC THENL
  [REWRITE_TAC[PERMUTED_COUNT; COUNT; COUNT_APPEND] THEN FIX_TAC "[y]" THEN
   HYP_TAC "sor1: c1 _" (REWRITE_RULE[SORT; PERMUTED_COUNT]) THEN
   HYP_TAC "sor2: c2 _" (REWRITE_RULE[SORT; PERMUTED_COUNT]) THEN
   REMOVE_THEN "c1" (fun th -> REWRITE_TAC[GSYM th]) THEN
   REMOVE_THEN "c2" (fun th -> REWRITE_TAC[GSYM th]) THEN
   REWRITE_TAC[COUNT_FILTER] THEN
   ASM_CASES_TAC `le (y:A) (x:A):bool` THEN ASM_REWRITE_TAC[] THEN ARITH_TAC;
   ALL_TAC] THEN
  HYP_TAC "sor1: fil1 ord1" (REWRITE_RULE[SORT]) THEN
  HYP_TAC "sor2: fil2 ord2" (REWRITE_RULE[SORT]) THEN
  ASM_REWRITE_TAC[ORDERED_APPEND; ORDERED_CONS] THEN
  CONJ_TAC THENL
  [REMOVE_THEN "fil2" (fun th -> REWRITE_TAC[MATCH_MP PERMUTED_ALL
       (ONCE_REWRITE_RULE[PERMUTED_SYM] th)]) THEN
   REWRITE_TAC[ALL_FILTER; GSYM ALL_MEM] THEN
   HYP MESON_TAC "total" [];
   ALL_TAC] THEN
  REMOVE_THEN "fil1" (fun th -> REWRITE_TAC[MATCH_MP PERMUTED_ALL
      (ONCE_REWRITE_RULE[PERMUTED_SYM] th)]) THEN
  REWRITE_TAC[ALL_FILTER] THEN GEN_REWRITE_TAC I [GSYM ALL_MEM] THEN
  REWRITE_TAC[ALL] THEN INTRO_TAC "![y]; y; xy" THEN
  ASM_REWRITE_TAC[] THEN
  REMOVE_THEN "fil2" (fun th -> REWRITE_TAC[MATCH_MP PERMUTED_ALL
      (ONCE_REWRITE_RULE[PERMUTED_SYM] th)]) THEN
  REWRITE_TAC[ALL_FILTER; GSYM ALL_MEM] THEN ASM_MESON_TAC[]);;

let NUM_QUICKSORT_HORN = prove
 (`SORT ((<=):num->num->bool) [] [] /\
   (!x xs ys xs1 xs2 ys1 ys2.
      FILTER ((>=) x) xs = xs1 /\
      SORT (<=) xs1 ys1 /\
      FILTER ((<) x) xs = xs2 /\
      SORT (<=) xs2 ys2 /\
      APPEND ys1 (CONS x ys2) = ys
      ==> SORT ((<=):num->num->bool) (CONS x xs) ys)`,
  MP_TAC (ISPEC `((<=):num->num->bool)` QUICKSORT_HORN) THEN
  ANTS_TAC THENL [ARITH_TAC; ALL_TAC] THEN
  SUBGOAL_THEN `!x:num. (\y. y <= x) = ((>=) x)`
    (fun th -> REWRITE_TAC[th]) THENL
  [GEN_TAC THEN REWRITE_TAC[FUN_EQ_THM; GE]; ALL_TAC] THEN
  SUBGOAL_THEN `!x:num. (\y. ~(y <= x)) = ((<) x)`
    (fun th -> REWRITE_TAC[th]) THEN
  GEN_TAC THEN REWRITE_TAC[FUN_EQ_THM; NOT_LE]);;

let quicksort_solver =
  solve (PROLOG_SLV NO_CONV
                    [NUM_QUICKSORT_HORN; NUM_APPEND_HORN; NUM_FILTER_HORN;
                     ARITH_LT_HORN; ARITH_LE_HORN]);;

(* Examples. *)

take 10 (quicksort_solver `??ys. SORT (<=) [] ys`);;
take 10 (quicksort_solver `??ys. SORT (<=) [3] ys`);;
take 10 (quicksort_solver `??ys. SORT (<=) [3;1] ys`);;
take 10 (quicksort_solver `??ys. SORT (<=) [1;3] ys`);;
take 10 (quicksort_solver `??ys. SORT (<=) [3;3] ys`);;
take 10 (quicksort_solver `??ys. SORT (<=) [12;3;3;23;7;9;12] ys`);;

take 10 (quicksort_solver `??ys. SORT (<=) [12;3;3;23;7;9;12] (CONS 3 ys)`);;
take 10 (quicksort_solver `??ys. SORT (<=) [12;3;3;23;7;9;12] (CONS 12 ys)`);;
take 10 (quicksort_solver `??y ys. SORT (<=) [12;3;3;23;7;9;12] (CONS y ys)`);;
take 10 (quicksort_solver `??ys zs. SORT (<=) [7;3;0] (APPEND ys zs)`);;

(* With type instantiation. *)

let quicksort_solver =
  solve (PROLOG_SLV NO_CONV
                    [NUM_QUICKSORT_HORN; APPEND_HORN; FILTER_HORN;
                     ARITH_LT_HORN; ARITH_LE_HORN]);;

take 10 (quicksort_solver `??ys. SORT (<=) [] ys`);;
take 10 (quicksort_solver `??ys. SORT (<=) [3] ys`);;
take 10 (quicksort_solver `??ys. SORT (<=) [3;1] ys`);;
take 10 (quicksort_solver `??ys. SORT (<=) [1;3] ys`);;
take 10 (quicksort_solver `??ys. SORT (<=) [3;3] ys`);;
take 10 (quicksort_solver `??ys. SORT (<=) [12;3;3;23;7;9;12] ys`);;

take 10 (quicksort_solver `??ys. SORT (<=) [12;3;3;23;7;9;12] (CONS 3 ys)`);;
take 10 (quicksort_solver `??ys. SORT (<=) [12;3;3;23;7;9;12] (CONS 12 ys)`);;
take 10 (quicksort_solver `??y ys. SORT (<=) [12;3;3;23;7;9;12] (CONS y ys)`);;
take 10 (quicksort_solver `??ys zs. SORT (<=) [7;3;0] (APPEND ys zs)`);;
