(* ========================================================================= *)
(* Semicertified interactive logic programming.                              *)
(*                                                                           *)
(* Copyright (c) 2017-2018 Marco Maggesi, Massimo Nocentini                  *)
(* ========================================================================= *)

needs "kanren-light/streams.hl";;

type mgoal = term list * goal;;

type solver = mgoal -> goalstate stream;;

type thm_solver = thm -> solver;;

type thm_hosolver = thm_solver -> thm_solver;;

(* ------------------------------------------------------------------------- *)
(* Meta goals.                                                               *)
(* ------------------------------------------------------------------------- *)

parse_as_binder "??";;
parse_as_infix ("?-",(0,"right"));;
parse_as_infix ("&&",(1,"right"));;

let META = new_definition `(??) = (?):(A->bool)->bool`;;
let ENTAILS = new_definition `(?-) = (==>)`;;
let MAND = new_definition `(&&) = (/\)`;;

let is_meta = is_binder "??";;
let dest_meta = dest_binder "??";;
let strip_meta = splitlist dest_meta;;

let is_mand = is_binary "&&";;
let dest_mand = dest_binary "&&";;
let split_mand = striplist dest_mand;;

let is_entails = is_binary "?-";;
let dest_entails = dest_binary "?-";;

let dest_mgoal (tm:term) : mgoal =
  let mvs,rtm = strip_meta tm in
  let hpl,w = try let btm,htm = dest_entails rtm in
                  rev(split_mand btm),htm
              with Failure _ -> [],rtm in
  let asl = map (fun tm -> "",ASSUME tm) hpl in
  mvs,(asl,w);;

(* ------------------------------------------------------------------------- *)
(* Run a solver.                                                             *)
(* ------------------------------------------------------------------------- *)

let wrapup : goalstate -> (term list * instantiation) * thm  =
  fun metas,gls,just ->
    if gls <> [] then failwith "wrapup: Unsolved goals" else
    let th = just null_inst [] in
    metas,th;;

let solve : solver -> term -> (term list * thm) stream =
  let run_solver g slv = stream_mapfilter wrapup (slv g) in
  let mk_eq i v = mk_eq(v,instantiate i v) in
  fun slv tm ->
    let g = dest_mgoal tm in
    let p ((_,inst),th) = map (mk_eq inst) (fst g),th in
    stream_map p (run_solver (dest_mgoal tm) slv);;

(* ------------------------------------------------------------------------- *)
(* Common pattern for building "singleton" solvers.                          *)
(* ------------------------------------------------------------------------- *)

let SOLVER (f:term list -> goal -> goalstate) : solver =
  fun mvs,gl -> try singleton (f mvs gl) with Failure _ -> Empty;;

(* ------------------------------------------------------------------------- *)
(* Basic solvers.                                                            *)
(* ------------------------------------------------------------------------- *)

let NO_SLV : solver =
  fun _ -> Empty;;

let REFL_SLV =
  SOLVER (fun mvs (asl,w) ->
    let l,r = dest_eq w in
    let inst = term_ty_unify mvs l r in
    let sth = ALPHA (instantiate inst l) (instantiate inst r) in
    let just i _ = INSTANTIATE_ALL i sth in
    ([],inst),[],just);;

(* ------------------------------------------------------------------------- *)
(* Refresh type variables in a theorem.                                      *)
(* ------------------------------------------------------------------------- *)

let genty =
  let count = ref 0 in
  let genty () =
    let ty = mk_vartype("ty"^string_of_int !count) in
    count := !count + 1;
    ty in
  genty;;

let TYFRESH th =
  let tyl = type_vars_in_term (concl th) in
  let i = map (fun ty -> genty(),ty) tyl in
  INST_TYPE i th;;

let SPEC_FRESH th =
  let th = TYFRESH th in
  let v,th' = SPEC_VAR th in
  let v' = genvar(type_of v) in
  v',INST [v',v] th';;

let SPEC_FRESH_ALL = splitlist SPEC_FRESH;;

let ACCEPT_SLV : thm_solver =
  fun th -> SOLVER (fun mvs (asl,w) ->
    let avs,th' = SPEC_FRESH_ALL th in
    let insts = term_ty_unify (union avs mvs) (concl th') w in
    let sth = INSTANTIATE_ALL insts th' in
    let just i _ = INSTANTIATE_ALL i sth in
    let meta = mapfilter (check is_var o instantiate insts) avs,insts in
    meta,[],just);;

let RULE_SLV : thm_solver = fun th ->
 if not(is_imp(snd(strip_forall(concl th)))) then
   failwith "RULE_SLV: Bad theorem"
 else
   SOLVER (fun mvs (asl,w) ->
     let avs,th = SPEC_FRESH_ALL th in
     let ant,con = dest_imp(concl th) in
     let insts = term_ty_unify (union avs mvs) con w in
     let sth = INSTANTIATE_ALL insts th in
     let lant = fst(dest_imp(concl sth)) in
     let just i (th :: _) =
       try MP (INSTANTIATE_ALL i sth) th
       with Failure _ ->
         (report(string_of_thm (CONJ (INSTANTIATE_ALL i sth) th));
          fail())
       in
     (* let _,vinst,_ = insts in
        let avs' = filter (fun v -> can (rev_assoc v vinst)) avs in
        let meta = avs',insts in *)
     let meta = avs,insts in
     meta,[asl,lant],just);;

let SAFE_RULE_SLV : thm_solver =
  fun th -> try RULE_SLV th with Failure _ -> NO_SLV;;

let CHANGED_SLV (slv:solver) : solver =
  fun (_,g as mg) ->
    bind (slv mg)
      (function
         meta,[gl],_ when meta = null_meta && equals_goal gl g -> Empty
       | gstate -> singleton gstate);;

(* ------------------------------------------------------------------------- *)
(* Solvers associated to tactics.                                            *)
(* ------------------------------------------------------------------------- *)

let TACTIC_SLV (tac:tactic) : solver =
  SOLVER (fun _ gl -> tac gl);;

let THM_TACTIC_SLV (ttac:thm_tactic) : thm_solver =
  fun th -> try let tac = ttac th in TACTIC_SLV tac
            with Failure _ -> NO_SLV;;

(* ------------------------------------------------------------------------- *)
(* Further basic solvers.                                                    *)
(* ------------------------------------------------------------------------- *)

let UNDISCH_THEN_SLV : term -> thm_solver -> solver =
  fun tm tslv (mvs,(asl,w)) ->
    let thp,asl' = remove (fun (_,th) -> aconv (concl th) tm) asl in
    tslv (snd thp) (mvs,(asl',w));;

(* ------------------------------------------------------------------------- *)
(* Metas.                                                                    *)
(* ------------------------------------------------------------------------- *)

let META_EXISTS_SLV : term -> term list -> term -> solver =
  fun v cvs t ->
    SOLVER (fun mvs (asl,w) ->
      if not (mem v mvs) then fail() else
      let mvs' = subtract (frees t) cvs in
      let vsubs = [t,v] in
      let vinst = [],vsubs,[] in
      let just i [th] = INSTANTIATE_ALL (compose_insts i vinst) th in
      (mvs',vinst),[asl,vsubst vsubs w],just);;

(* ------------------------------------------------------------------------- *)
(* Solver combinators.                                                       *)
(* ------------------------------------------------------------------------- *)

let THEN_SLV,THENL_SLV =
  let propagate_empty i [] = []
  and propagate_thm th i [] = INSTANTIATE_ALL i th in
  let compose_justs n just1 just2 i ths =
    let ths1,ths2 = chop_list n ths in
    (just1 i ths1)::(just2 i ths2) in
  let rec seqapply mvs l1 l2 = match (l1,l2) with
     ([],[]) -> singleton(null_meta,[],propagate_empty)
   | ((slv:solver)::slvs),((goal:goal)::goals) ->
       bind (slv (mvs,goal)) (fun (mvs1,insts1),gls1,just1 ->
         let goals' = map (inst_goal insts1) goals in
         stream_map
           (fun (mvs2,insts2),gls2,just2 ->
             let just1' i = just1 (compose_insts insts2 i) in
             ((union mvs1 mvs2,compose_insts insts1 insts2),
              gls1@gls2,compose_justs (length gls1) just1' just2))
           (seqapply (union mvs1 mvs) slvs goals'))
   | _,_ -> failwith "seqapply: Length mismatch" in
  let justsequence just1 just2 insts2 i ths =
    just1 (compose_insts insts2 i) (just2 i ths) in
  let slvsequence mvs ((mvs1,insts1),gls1,just1) slvl =
    stream_map (fun (mvs2,insts2),gls2,just2 ->
                  let jst = justsequence just1 just2 insts2 in
                  let just = if gls2 <> [] then jst else
                             propagate_thm (jst null_inst []) in
                  (union mvs1 mvs2,compose_insts insts1 insts2),gls2,just)
               (seqapply (union mvs1 mvs) slvl gls1) in
  let (THEN_SLV: solver -> solver -> solver) =
    fun slv1 slv2 (mvs,_ as g) ->
      bind (slv1 g) (fun _,gls,_ as gstate ->
        slvsequence mvs gstate (replicate slv2 (length gls)))
  and (THENL_SLV: solver -> solver list -> solver) =
    fun slv1 slv2l (mvs,_ as g) ->
      bind (slv1 g) (fun _,gls,_ as gstate ->
        if gls = [] then slvsequence mvs gstate []
        else slvsequence mvs gstate slv2l) in
  THEN_SLV,THENL_SLV;;

(* ------------------------------------------------------------------------- *)
(* Interactive usage of solvers.                                             *)
(* ------------------------------------------------------------------------- *)

type mgoalstate = goalstate stream;;

type mgoalstack = mgoalstate list;;

let current_mgoalstack : mgoalstack ref = ref [];;

let (mk_mgoalstate:mgoal->mgoalstate) =
  let just inst [th] = INSTANTIATE_ALL inst th in
  fun (mvs,(asl,w)) ->
    if type_of w = bool_ty then
      singleton((mvs,null_inst),[asl,w],just)
    else failwith "mk_mgoalstate: Non-boolean goal";;

let set_mgoal : mgoal -> mgoalstack =
  fun mgl ->
    current_mgoalstack := [mk_mgoalstate mgl];
    !current_mgoalstack;;

let gg tm =
  set_mgoal(dest_mgoal tm);;

let pp () : mgoalstack =
  !current_mgoalstack;;

let bb () : mgoalstack =
  if !current_mgoalstack = [] then failwith "No mgoal set." else
  current_mgoalstack := tl (!current_mgoalstack);
  !current_mgoalstack;;

let (mrefine:(mgoalstate->mgoalstate)->mgoalstack) =
  fun r ->
    let l = !current_mgoalstack in
    if l = [] then failwith "No current mgoal" else
    let h = hd l in
    let res = r h :: l in
    current_mgoalstack := res;
    !current_mgoalstack;;

let (mby:solver->(mgoalstate->mgoalstate)) =
  fun slv mgstate ->
    bind mgstate
      (fun ((mvs,inst),gls,just) ->
        if gls = [] then failwith "No goal set" else
        let g = hd gls
        and ogls = tl gls in
        stream_map
          (fun ((newmvs,newinst),subgls,subjust) ->
            let n = length subgls in
            let mvs' = union newmvs mvs
            and inst' = compose_insts inst newinst
            and gls' = subgls @ map (inst_goal newinst) ogls in
            let just' i ths =
              let i' = compose_insts inst' i in
              let cths,oths = chop_list n ths in
              let sths = (subjust i cths) :: oths in
              just i' sths in
            (mvs',inst'),gls',just')
          (slv (mvs,g)));;

(* MVALID ? *)
(*  let e tac = refine(by(VALID tac));; *)

let ee (slv:solver) : mgoalstack =
  mrefine(mby slv);;

let ss () =
  let gs = next(hd !current_mgoalstack) in
  current_mgoalstack := gs :: !current_mgoalstack;
  !current_mgoalstack;;

let tt : tactic -> mgoalstack =
  fun tac -> ee (TACTIC_SLV tac);;

let top_thms : unit -> (instantiation * thm) stream =
  let safe_wrapup gstate =
    try wrapup gstate with Failure s ->
      (report ("wrapup error: "^s);
       ([],null_inst),TRUTH) in
  let p gstate =
    let (mvs,i),th = safe_wrapup gstate in
    i,th in
  fun () ->
    match !current_mgoalstack with
      [] -> failwith "top_thms: Goalstack empty"
    | s :: _ -> stream_map p s;;

let top_thm1() : instantiation * thm =
  get (top_thms());;

(*
gg `??X. 2 + X = 4`;;
ee (TACTIC_SLV (REWRITE_TAC[ARITH_RULE `4 = 2 + 2`]));;
ee REFL_SLV;;
top_thm1();;
*)

(* ------------------------------------------------------------------------- *)
(* Printer for mgoalstacs.                                                   *)
(* ------------------------------------------------------------------------- *)

let print_goalstate : goalstate -> unit =
  let print_var v = print_qterm v; Format.print_string ", " in
  let print_metavars mvl =
    Format.print_string "Metavariables: "; 
    do_list print_var mvl;
    Format.print_newline() in
  fun gs ->
    let ((mvl,_),gl,_) = gs in
    if gl = [] then
      (Format.print_string "No sub(m)goals"; Format.print_newline())
    else
      (do_list print_goal (rev gl);
       print_metavars mvl);;

let print_mgoalstack:mgoalstack->unit =
  fun mgls ->
    if mgls = [] then Format.print_string "Empty mgoalstack" else
    let mgl = hd mgls in
    if mgl = Empty then Format.print_string "No (more) solutions" else
    print_goalstate (get mgl);;

#install_printer print_mgoalstack;;

(* ------------------------------------------------------------------------- *)
(* Sequential chains of solvers.                                             *)
(* ------------------------------------------------------------------------- *)

let EVERY_SLV slvl =
  itlist THEN_SLV slvl (TACTIC_SLV ALL_TAC);;

(* TODO: Better version?
let EVERY_SLV slvl =
  if slvl = [] then TACTIC_SLV ALL_TAC else
  end_itlist THEN_SLV slvl;;
*)

let MAP_EVERY_SLV slvf lst =
  EVERY_SLV (map slvf lst);;

(* ------------------------------------------------------------------------- *)
(* Manipulation of assumption list.                                          *)
(* ------------------------------------------------------------------------- *)

let FIND_ASSUM_SLV : thm_solver -> term -> solver =
  fun tslv t (_,(asl,_) as g) ->
    tslv(snd(find (fun (_,th) -> concl th = t) asl)) g;;

let POP_ASSUM_SLV : thm_solver -> solver =
  fun tslv ->
    function
      mvs,(((_,th)::asl),w) -> tslv th (mvs,(asl,w))
    | _ -> Empty;;

let ASSUM_LIST_SLV : (thm list -> solver) -> solver =
  fun aslfun (_,(asl,_) as g) -> aslfun (map snd asl) g;;

let POP_ASSUM_LIST_SLV : (thm list -> solver) -> solver =
  fun aslslv (mvs,(asl,w)) -> aslslv (map snd asl) (mvs,([],w));;

let EVERY_ASSUM_SLV : thm_solver -> solver =
  fun tslv -> ASSUM_LIST_SLV (MAP_EVERY_SLV tslv);;

(* ------------------------------------------------------------------------- *)
(* Concatenation of solvers.                                                 *)
(* ------------------------------------------------------------------------- *)

let CONCAT_SLV (slv1:solver) (slv2:solver) : solver =
  fun g -> concatf (slv1 g) (fun _ -> slv2 g);;

let COLLECT_SLV (slvl:solver list) : solver =
  itlist CONCAT_SLV slvl NO_SLV;;

let MAP_COLLECT_SLV (slvf:'a->solver) (lst:'a list) : solver =
  COLLECT_SLV (map slvf lst);;

let COLLECT_ASSUM_SLV (tslv:thm_solver) : solver =
  fun (mvs,(asl,w) as g) -> MAP_COLLECT_SLV (tslv o snd) asl g;;

let COLLECT_X_ASSUM_SLV (tslv:thm_solver) : solver =
  COLLECT_ASSUM_SLV (fun th -> UNDISCH_THEN_SLV (concl th) tslv);;

let MAYBE_SLV (slv : solver) : solver =
  CONCAT_SLV slv (TACTIC_SLV ALL_TAC);;

let rec REPEAT_SLV (slv : solver) : solver =
  fun g ->
    CONCAT_SLV (THEN_SLV slv (REPEAT_SLV slv)) (TACTIC_SLV ALL_TAC) g;;

let rec LAZY_REPEAT_SLV (slv : solver) : solver =
  fun g ->
    CONCAT_SLV (TACTIC_SLV ALL_TAC) (THEN_SLV slv (LAZY_REPEAT_SLV slv)) g ;;

let APPLY_SLV : thm_solver =
  fun th -> try let rule = RULE_SLV th in
                CONCAT_SLV (ACCEPT_SLV th) rule
            with Failure _ -> ACCEPT_SLV th;;

(* ------------------------------------------------------------------------- *)
(* Interleaving of solvers.                                                  *)
(* ------------------------------------------------------------------------- *)

let MPLUS_SLV (slv1:solver) (slv2:solver) : solver =
  fun g -> mplusf (slv1 g) (fun _ -> slv2 g);;

(* OLD VERSION: è sbilanciato verso l'inizio della lista. *)
(*
let INTERLEAVE_SLV (slvl:solver list) : solver =
  if slvl = [] then NO_SLV else end_itlist MPLUS_SLV slvl;;
*)

let INTERLEAVE_SLV (slvl:solver list) : solver =
  if slvl = [] then NO_SLV else
  mergef_stream slvl [];;

let MAP_INTERLEAVE_SLV (slvf:'a->solver) (lst:'a list) : solver =
  INTERLEAVE_SLV (map slvf lst);;

let INTERLEAVE_ASSUM_SLV (tslv:thm_solver) : solver =
  fun (mvs,(asl,w) as g) -> MAP_INTERLEAVE_SLV (tslv o snd) asl g;;

let INTERLEAVE_X_ASSUM_SLV (tslv:thm_solver) : solver =
  INTERLEAVE_ASSUM_SLV (fun th -> UNDISCH_THEN_SLV (concl th) tslv);;

(* ------------------------------------------------------------------------- *)
(* A simple Prolog-like solver.                                              *)
(* ------------------------------------------------------------------------- *)

parse_as_infix("IS",(10,"right"));;

let IS_DEF = new_definition
  `(IS) = (=):A->A->bool`;;

let IS_SLV : conv -> solver =
  let IS_TAC : conv -> tactic =
    let is_is = is_binary "IS" in
    let is_thm = prove(`!x y:A. x IS y <=> x = y`, REWRITE_TAC[IS_DEF]) in
    fun conv (_,w) as g ->
      if not (is_is w) then failwith "IS_TAC" else
      (CONV_TAC (RAND_CONV conv) THEN GEN_REWRITE_TAC I [is_thm]) g in
  fun conv -> THEN_SLV (TACTIC_SLV (IS_TAC conv)) REFL_SLV;;

let PROLOG_SLV conv thl : solver =
  let RAW_PROLOG_SLV thl : solver =
    let rec PROLOG_SLV g =
      CONCAT_SLV (IS_SLV conv) (MAP_COLLECT_SLV STEP_SLV thl) g
    and STEP_SLV : thm_solver =
      fun th ->
        try let rule = RULE_SLV th in
            EVERY_SLV [rule; TACTIC_SLV (REPEAT CONJ_TAC); PROLOG_SLV]
        with Failure _ -> ACCEPT_SLV th in
    THEN_SLV (TACTIC_SLV (REPEAT CONJ_TAC)) PROLOG_SLV in
  let thl' = itlist (fun th acc -> CONJUNCTS th @ acc) thl [] in
  RAW_PROLOG_SLV thl';;

(* ------------------------------------------------------------------------- *)
(* Tests and examples.                                                       *)
(* ------------------------------------------------------------------------- *)

print_endline "Inizio interazione.";;

let sols = take 10
  (solve (POP_ASSUM_LIST_SLV (PROLOG_SLV NO_CONV))
         `??a b:A.
             (!x y z. padre x y /\ antenato y z ==> antenato x z) &&
             (!x y. padre x y ==> antenato x y) &&
             padre pippo pluto && padre pluto paperino
             ?- antenato a b`);;
map (concl o snd) sols;;

let sols = take 10
  (solve (POP_ASSUM_LIST_SLV (PROLOG_SLV NO_CONV))
         `??a b:A.
             (!x y z. padre x y /\ antenato y z ==> antenato x z) &&
             (!x. antenato x x) &&
             padre pippo pluto && padre pluto paperino
             ?- antenato a b`);;
map (concl o snd) sols;;

let sols = take 10
  (solve (POP_ASSUM_LIST_SLV (PROLOG_SLV NO_CONV))
         `??a b:A.
             (!x y z. persona z /\ padre x y /\ antenato y z ==> antenato x z) &&
             (!x. persona x ==> antenato x x) &&
             padre pippo pluto && padre pluto paperino &&
             persona pippo && persona pluto && persona paperino
             ?- antenato a b`);;
map (concl o snd) sols;;

(setify o map (concl o snd) o take 100 o
 solve (POP_ASSUM_LIST_SLV (PROLOG_SLV NO_CONV)))
  `??a b:A.
      (!x y z. antenato x y /\ antenato y z ==> antenato x z) &&
      (!x. antenato x x) &&
      (!x y. padre x y ==> antenato x y) &&
      padre pippo pluto && padre pluto paperino
      ?- antenato a b`;;

print_endline "Fine interazione.";;

(* ------------------------------------------------------------------------- *)
(* Esempio di un caso in cui con le eccezioni non si troverebbe nessuna      *)
(* soluzione.                                                                *)
(* Infatti, con le eccezioni, dopo aver trovato la soluzione parziale        *)
(* `padre a b`, non si saprebbe come continuare.                             *)
(* In questo caso, il punto cruciale è come procedere per risolvere una      *)
(* congiunzione: la struttura """CONJ_TAC THEN PROLOG_TAC""" non permette,   *)
(* una volta trovata una unficazione sulla prima "branch" di ritrattarla se  *)
(* la seconda branch fallisce.                                               *)
(* ------------------------------------------------------------------------- *)

(*
ITAUT `(!x y z. padre x y /\ padre y z ==> nonno x z) /\
       padre c d /\
       padre a c /\
       padre a b
       ==> ?x:A. nonno a x`;;

gg `??x:A. (!x y z. padre x y /\ padre y z ==> nonno x z) &&
           padre c d &&
           padre a c &&
           padre a b
           ?- nonno a x`;;
ee (RULE_SLV (ASSUME `!x y z. padre x y /\ padre y z ==> nonno x z`));;
tt CONJ_TAC;;
ee (ACCEPT_SLV (ASSUME `padre (a:A) (c:A) : bool`));;
ee (ACCEPT_SLV (ASSUME `padre (c:A) (d:A) : bool`));;
top_thm1();;

gg `??x:A. (!x y z. padre x y /\ padre y z ==> nonno x z) &&
           padre c d &&
           padre a c &&
           padre a b
           ?- nonno a x`;;
ee (POP_ASSUM_LIST_SLV (PROLOG_SLV NO_CONV));;
top_thm1();;
*)
